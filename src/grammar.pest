// Pest syntax: 
// https://docs.rs/pest_derive/2.1.0/pest_derive/
// https://pest.rs/book/grammars/syntax.html

whole_expression = _{ SOI ~ expression ~ EOI }

plus            = { "+" }
minus           = { "-" }
star            = { "*" }
slash           = { "/" }
equal           = { "=" }
equal_equal     = { "==" }
bang            = { "!" }
bang_equal      = { "!=" }
greater         = { ">" }
greater_equal   = { ">=" }
less            = { "<" }
less_equal      = { "<=" }
and             = { "&&" }
or              = { "||" }

unary_operator      =  { bang | minus }
logical_operator    =  _{ and | or }
binary_operator     =  _{ plus | minus | star | slash | equal_equal | bang_equal | greater | 
                          greater_equal | less | less_equal }

expression = { logical | binary | unary }
    logical     = { binary ~ (logical_operator ~ binary)* }
    binary      = { unary ~ (binary_operator ~ unary)* }
    unary       = { (unary_operator ~ unary) | primary }
    primary     = _{ boolean | float | integer | "(" ~ expression ~ ")" }

float = ${ float_characteristic ~ "." ~ float_mantissa }
    float_characteristic = { "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
    float_mantissa = { ASCII_DIGIT+ }

integer = ${ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

boolean = { "true" | "false" }

WHITESPACE = _{ " " | "\t" }